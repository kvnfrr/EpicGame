plugins {
    id 'java'
    id 'eclipse'
    id 'application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    main {
        java {
            srcDirs = ["src/"]
        }
        resources {
            srcDirs = ["../assets"]
        }
    }
}

application {
    mainClass = "com.epic.game.DesktopLauncher"
}

project.ext.assetsDir = new File("../assets")

import org.gradle.internal.os.OperatingSystem

tasks.named('run', JavaExec).configure {
    dependsOn classes
    mainClass = application.mainClass.get()
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

tasks.register('debug', JavaExec) {
    dependsOn classes
    mainClass = application.mainClass.get()
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

tasks.register('dist', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

tasks.named('dist').configure {
    dependsOn classes
}

eclipse {
    project {
        name = appName + "-desktop"
    }
}